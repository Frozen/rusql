// vim:set et sw=4 ts=4 ft=rust:
// Adapted from: https://github.com/steveyen/sqld3/blob/master/sql.pegjs
// And: https://www.sqlite.org/lang.html

use definitions::*;

#[pub]
rusql_stmt -> RusqlStatement
        = whitespace s:(alter_table_stmt) whitespace semicolon { s }
        / whitespace s:(create_table_stmt) whitespace semicolon { s }
        / whitespace s:(delete_stmt) whitespace semicolon { s }
        / whitespace s:(drop_table_stmt) whitespace semicolon { s }
        / whitespace s:(insert_stmt) whitespace semicolon { s }
        / whitespace s:(select_stmt) whitespace semicolon { s }
        / whitespace s:(update_stmt) whitespace semicolon { s }

#[pub]
rusql_parse -> Vec<RusqlStatement>
        = (r:rusql_stmt whitespace { r })+

//
// Statements
//

// ALTER TABLE
// https://www.sqlite.org/lang_altertable.html

alter_table_stmt -> RusqlStatement
        = ALTER TABLE n:table_name a:(rename_to / add_column) {
            let def = AlterTableDef {
                name: n,
                mode: a,
            };
            RusqlStatement::AlterTable(def)
        }

rename_to -> AlterTable
        = RENAME TO n:table_name { AlterTable::RenameTo(n) }

add_column -> AlterTable
        = ADD COLUMN? c:column_def { AlterTable::AddColumn(c) }

// CREATE TABLE
// https://www.sqlite.org/lang_createtable.html

create_table_stmt -> RusqlStatement
        = CREATE TABLE i:(IF NOT EXISTS)?
                n:table_name whitespace lparen c:(column_def ++ comma) rparen {
            let def = TableDef {
                table_name: n,
                columns: c,
                if_not_exists: i.is_some(),
            };
            RusqlStatement::CreateTable(def)
          }

column_def -> ColumnDef
        = n:column_name t:(type_name)? c:(column_constraint)* {
            ColumnDef {
                name: n,
                column_type: t,
                column_constraints: c,
            }
        }

column_constraint -> ColumnConstraint
        = (CONSTRAINT name)? (PRIMARY KEY) {
            ColumnConstraint::PrimaryKey
        }

// DELETE
// https://www.sqlite.org/lang_delete.html
delete_stmt -> RusqlStatement
        = DELETE FROM n:qualified_table_name w:where_expr? {
            let def = DeleteDef {
                name: n,
                where_expr: w,
            };
            RusqlStatement::Delete(def)
        }

// DROP TABLE
// https://www.sqlite.org/lang_droptable.html

drop_table_stmt -> RusqlStatement
        = DROP TABLE n:table_name {
            let def = DropTableDef {
                name: n,
            };
            RusqlStatement::DropTable(def)
        }

// INSERT
// https://www.sqlite.org/lang_insert.html

insert_stmt -> RusqlStatement
        = INSERT INTO n:table_name cn:insert_column_name? d:insert_data_source {
            let def = InsertDef {
                table_name: n,
                column_names: cn,
                data_source: d,
            };
            RusqlStatement::Insert(def)
        }

insert_data_source -> InsertDataSource
        = VALUES v:(insert_values ++ comma) { InsertDataSource::Values(v) }
        / s:select_stmt {
            match s {
                RusqlStatement::Select(stmt) => InsertDataSource::Select(stmt),
                _ => InsertDataSource::Error,
            }
        }
        / DEFAULT VALUES { InsertDataSource::DefaultValues }

insert_column_name -> Vec<String>
        = whitespace lparen s:(column_name ++ comma) rparen { s }

insert_values -> Vec<LiteralValue>
        = whitespace lparen l:(literal_value ++ comma) rparen { l }

// SELECT
// https://www.sqlite.org/lang_select.html

select_stmt -> RusqlStatement
        = SELECT r:result_column
                f:from_clause?
                w:where_expr?
                o:(ORDER BY o:(ordering_term ++ comma) { o })? {
            let def = SelectDef {
                result_column: r,
                from_clause: f,
                where_expr: w,
                ordering_terms: o,
            };
            RusqlStatement::Select(def)
        }

from_clause -> FromClause
        = FROM t:table_or_subquery j:join_clause+ {
            FromClause::JoinClause(t, Some(j))
        }
        / FROM t:(table_or_subquery ++ comma) { FromClause::TableOrSubquery(t) }

join_operator -> JoinOperator
        = JOIN { JoinOperator::Inner }
        / NATURAL JOIN { JoinOperator::Natural }

join_clause -> JoinClause
        = j:join_operator t:table_or_subquery c:join_constraint? { (j, t, c) }

join_constraint -> JoinConstraint
        = ON e:expr { JoinConstraint::On(e) }

result_column -> ResultColumn
        = whitespace1 a:asterisk { ResultColumn::Asterisk }
        / whitespace1 e:(expr ++ comma) { ResultColumn::Expressions(e) }

table_or_subquery -> String
        = table_name

ordering_term -> OrderingTerm
        = e:expr /*(COLLATE collation_name)?*/ o:asc_or_desc? {
            OrderingTerm {
                expr: e,
                order: if let Some(order) = o { order } else { Order::Ascending }
            }
        }

asc_or_desc -> Order
        = ASC { Order::Ascending }
        / DESC { Order::Descending }

// UPDATE
// https://www.sqlite.org/lang_update.html

update_stmt -> RusqlStatement
        = UPDATE n:qualified_table_name SET s:(update_column_equals ++ comma) w:where_expr? {
            let def = UpdateDef {
                name: n,
                set: s,
                where_expr: w,
            };
            RusqlStatement::Update(def)
        }

update_column_equals -> (String, Expression)
        = n:column_name equals e:expr { (n, e) }

qualified_table_name -> String
        = table_name

//
// Expressions
// https://www.sqlite.org/lang_expr.html
//

value -> Expression
    = whitespace e:(
        l:literal_value { Expression::LiteralValue(l) }
        / lparen e:expr rparen { e }
        / t:table_name dot n:column_name { Expression::TableName((t, box Expression::ColumnName(n))) }
        / !unary_operator n:column_name { Expression::ColumnName(n) }
        / u:unary_operator e:expr { Expression::UnaryOperator((u, box e)) }
    ) { e }

expr -> Expression
        = whitespace (
            e1:value b:binary_operator e2:expr { Expression::BinaryOperator((b, box e1, box e2)) }
            / v:value { v }
        )

where_expr -> Expression
        = WHERE whitespace1 e:expr { e }

binary_operator -> BinaryOperator
        = whitespace b:(
            asterisk { BinaryOperator::Mult }
            / slash { BinaryOperator::Divide }
            / percent { BinaryOperator::Modulo }
            / plus { BinaryOperator::Plus }
            / minus { BinaryOperator::Minus }
            / not_equals { BinaryOperator::NotEquals }
            / rshift { BinaryOperator::RShift }
            / lshift { BinaryOperator::LShift }
            / le { BinaryOperator::LessEq }
            / lt { BinaryOperator::Less }
            / ge { BinaryOperator::GreaterEq }
            / gt { BinaryOperator::Greater }
            / equals { BinaryOperator::Equals }
            / and { BinaryOperator::And }
            / or { BinaryOperator::Or }
            / band { BinaryOperator::BitAnd }
            / bor { BinaryOperator::BitOr }
        ) { b }

unary_operator -> UnaryOperator
        = plus { UnaryOperator::Plus }
        / minus { UnaryOperator::Minus }
        / not { UnaryOperator::Not }
        / bneg { UnaryOperator::BitNeg }

//////////////////////////////////////////////////////////////////////////////////////////////////

whitespace = [ \t\n\r]*
whitespace1 = [ \t\n\r]+
semicolon = ";"
lparen = "("
rparen = ")"
comma = ","
asterisk = "*"
equals = "==" / "="
not_equals = "!=" / "<>"
plus = "+"
minus = "-"
dot = "."
slash = "/"
percent = "%"
and = whitespace "AND"
not = whitespace "NOT"
or = whitespace "OR"
lt = "<"
gt = ">"
le = "<="
ge = ">="
band = "&"
bor = "|"
bneg = "~"
rshift = ">>"
lshift = "<<"

string_literal -> String
        = "\"" s:string "\"" { s }
        / "'" s:string_sq "'" { s }
string -> String
        = (escape_char / [^"])* { match_str.to_string() } //"// (syntax highlight fix)
string_sq -> String
        = (escape_char / [^'])* { match_str.to_string() }
escape_char = "\\" .

ALTER = whitespace "ALTER"
CREATE = whitespace "CREATE"
DELETE = whitespace "DELETE"
DROP = whitespace "DROP"
INSERT = whitespace "INSERT"
SELECT = whitespace "SELECT"
UPDATE = whitespace "UPDATE"

ADD = whitespace1 "ADD"
ASC = whitespace1 "ASC"
BY = whitespace1 "BY"
COLUMN = whitespace1 "COLUMN"
CONSTRAINT = whitespace1 "CONSTRAINT"
DEFAULT = whitespace1 "DEFAULT"
DESC = whitespace1 "DESC"
EXISTS = whitespace1 "EXISTS"
FROM = whitespace1 "FROM"
JOIN = whitespace1 "JOIN"
INTEGER = whitespace1 "INTEGER"
INTO = whitespace1 "INTO"
IF = whitespace1 "IF"
IS = whitespace1 "IS"
KEY = whitespace1 "KEY"
NATURAL = whitespace1 "NATURAL"
NOT = whitespace1 "NOT"
ON = whitespace1 "ON"
ORDER = whitespace1 "ORDER"
PRIMARY = whitespace1 "PRIMARY"
RENAME = whitespace1 "RENAME"
SET = whitespace1 "SET"
TABLE = whitespace1 "TABLE"
TEXT = whitespace1 "TEXT"
TO = whitespace1 "TO"
VALUES = whitespace1 "VALUES"
WHERE = whitespace1 "WHERE"

// no whitespace name
nws_name -> String
        = [A-Za-z0-9_]+ { match_str.to_string() }

name -> String
        = whitespace1 n:nws_name { n }

table_name -> String
        = whitespace n:nws_name { n }

column_name -> String
        = whitespace n:nws_name { n }

type_name -> ColumnType
        = INTEGER { ColumnType::Integer }
        / TEXT { ColumnType::Text }

literal_value -> LiteralValue
        = whitespace ([0-9]+ { LiteralValue::Integer(match_str.parse::<isize>().unwrap()) })
        / whitespace (s:string_literal { LiteralValue::Text(s) })
